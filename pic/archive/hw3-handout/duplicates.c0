bool all_distinct(string[] A, int n)
//@requires 0 <= n && n <= \length(A);
//@requires is_sorted(A, 0, n);
{
  for(int i = 0; i < n-1; i=i+1){
    if(string_equal(A[i], A[i+1])){
      return false;
    }
  }
  return true;
}

int count_distinct(string[] A, int n)
//@requires 0 <= n && n <= \length(A);
//@requires is_sorted(A, 0, n);
{
  if(n == 0) return 0;
  int count = 1;
  for(int i = 0; i < n-1; i=i+1){
    if(!string_equal(A[i], A[i+1])){
      count += 1;
    }
  }
  return count; 
}

string[] remove_duplicates(string[] A, int n)
//@requires 0 <= n && n <= \length(A);
//@requires is_sorted(A, 0, n);
//@ensures \length(\result) == count_distinct(A, n);
//@ensures all_distinct(\result, \length(\result));
//@ensures is_sorted(\result, 0, \length(\result));
{
  int dist_size = count_distinct(A, n);
  string[] result = alloc_array(string, dist_size);
  if(dist_size == 0){
    return result;
  }
  result[0] = A[0];
  int iter = 1;
  for(int i = 1; i < n; i+=1)
  //@loop_invariant count_distinct(A, i) == iter;
  //@loop_invariant all_distinct(result, iter);
  {
    if(!string_equal(A[i-1], A[i])){
      result[iter] = A[i];
      iter += 1;
    }
  }
  //@assert iter == dist_size;
  return result;
}